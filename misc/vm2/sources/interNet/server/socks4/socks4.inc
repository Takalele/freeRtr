Function doConn(var con:OneConnectionRecord;conN:LongInt):Boolean;
Label f1;
Var
  buf:array[1..4*1024] of byte;
  i,o:LongInt;
  p1,p2,t1,t2,r1,r2:LongInt;
  a,b:String;
Begin;
doConn:=False;
if (GetTimePast(con.time)>60*5) then begin;
  con.stat:=0;
  doConn:=True;
  exit;
  end;
case con.stat of
  5:begin; {connected}
    pipeLineStats(con.pipe1,p1,r1,t1);
    pipeLineStats(con.pipe2,p2,r2,t2);
    if (p1=0) then if (r1<1) then goto f1;
    if (p2=0) then if (r2<1) then goto f1;
    if (r1>t2) then r1:=t2;
    if (r1>sizeof(buf)) then r1:=sizeof(buf);
    if (r1>0) then begin;
      pipeLineRecv(con.pipe1,buf,r1);
      pipeLineSend(con.pipe2,buf,r1);
      con.time:=CurrentTime;
      end;
    if (r2>t1) then r2:=t1;
    if (r2>sizeof(buf)) then r2:=sizeof(buf);
    if (r2>0) then begin;
      pipeLineRecv(con.pipe2,buf,r2);
      pipeLineSend(con.pipe1,buf,r2);
      con.time:=CurrentTime;
      end;
    end;
  4:begin; {connecting}
    if TCPlookConnected(con.pipe2,a,i,o) then begin;
      if (con.pipe2<>0) then exit;
      WriteLn('connection refused by target');
      doConn:=true;
      exit;
      end;
    WriteLn('connection accepted by target, local side is '+ipAddr2string(a)+' '+BStr(i));
    fillChar(buf,8,0);
    buf[1]:=0;
    buf[2]:=90;
    pipeLineSend(con.pipe1,buf,8);
    con.time:=CurrentTime;
    con.stat:=5;
    end;
  3:begin; {user name}
    pipeLineStats(con.pipe1,p1,r1,t1);
    if (r1<1) then begin;
      if (p1=0) then goto f1;
      exit;
      end;
    i:=1;
    pipeLineRecv(con.pipe1,buf,i);
    if (buf[1]<>0) then exit;
    WriteLn('connecting to '+ipAddr2string(con.tadr)+' '+BStr(con.tprt));
    TCPbeginConnect(con.pipe2,65536,con.tadr,con.tprt);
    con.time:=CurrentTime;
    con.stat:=4;
    end;
  2:begin; {address}
    pipeLineStats(con.pipe1,p1,r1,t1);
    if (r1<8) then begin;
      if (p1=0) then goto f1;
      exit;
      end;
    i:=8;
    pipeLineRecv(con.pipe1,buf,i);
    if (buf[1]<>4) then begin;
      WriteLn('wrong protocol version!');
      goto f1;
      end;
    if (buf[2]<>1) then begin;
      WriteLn('wrong command code!');
      goto f1;
      end;
    con.tprt:=ReadWordMSB(buf[3]);
    a:=IPv4addressPrefix;
    move(buf[5],a[length(a)+1],4);
    move(a[1],con.tadr,sizeof(con.tadr));
    con.time:=CurrentTime;
    con.stat:=3;
    end;
  1:begin; {init}
    if TCPlookConnected(con.pipe1,con.cadr,con.cprt,i) then begin;
      doConn:=(con.pipe1=0);
      exit;
      end;
    o:=0;
    for i:=1 to relayListNum do if TestAddrMask(con.cadr,relayListDat[i].adr,relayListDat[i].msk) then inc(o);
    if (o=0) then begin;
      WriteLn('connection not allowed from '+ipAddr2string(con.cadr)+' '+BStr(con.cprt));
      goto f1;
      end;
    WriteLn('connection accepted from '+ipAddr2string(con.cadr)+' '+BStr(con.cprt));
    con.stat:=2;
    end;
  0:goto f1; {disconnect}
  else begin; f1: con.stat:=0;doConn:=True;exit; end;
  end;
End;
