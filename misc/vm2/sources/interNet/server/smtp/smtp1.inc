Function doConn(var con:OneConnectionRecord;conN:LongInt):Boolean;

Procedure sendResp(a:String);
Begin;
a:=a+#13#10;
pipeLineSend(con.pipe,a[1],length(a));
con.time:=CurrentTime;
End;

Procedure BeginRequest;
Begin;
if (con.fileN<>'') then exit;
CreateFile(TempPath,MsgdatExt,con.fileN);
xCreate(TempPath+con.fileN+LocalExt);
xCreate(TempPath+con.fileN+RemoteExt);
End;

Function getNextCommand:String;
Label f1;
Var
  i,o,p:LongInt;
  a,b:String;
Begin;
getNextCommand:='';
f1:
if (con.ibufP>=con.ibufS) then begin;
  i:=inputBufMax;
  if (pipeLineRecv(con.pipe,con.ibufD,i)<>0) then i:=0;
  con.ibufS:=i;
  con.ibufP:=0;
  if (i<1) then begin;
    pipeLineStats(con.pipe,i,o,p);
    if (i<>0) then exit;
    con.stat:=0;
    WriteLn('connection lost with '+ipAddr2string(con.adr)+' '+BStr(con.prt));
    exit;
    end;
  con.time:=CurrentTime;
  end;
inc(con.ibufP);
i:=con.ibufD[con.ibufP];
if (i=13) then goto f1;
if (i<>10) then begin;
  con.cmd:=con.cmd+chr(i);
  goto f1;
  end;
getNextCommand:=con.cmd;
con.cmd:='';
End;

Label f1;
Var
  t,tt:xtText;
  i,o,p:LongInt;
  a,b:String;
Begin;
if (GetTimePast(con.time)>5*60) then begin;
  WriteLn('connection timed out with '+ipAddr2string(con.adr)+' '+BStr(con.prt));
  sendResp('421 timeout!');
  doConn:=True;
  exit;
  end;
doConn:=False;
case con.stat of
  3:begin; {rx}
    f1:
    if (con.ibufP>=con.ibufS) then begin;
      i:=inputBufMax;
      if (pipeLineRecv(con.pipe,con.ibufD,i)<>0) then i:=0;
      con.ibufS:=i;
      con.ibufP:=0;
      if (i<1) then begin;
        pipeLineStats(con.pipe,i,o,p);
        if (i<>0) then exit;
        con.stat:=0;
        WriteLn('connection lost with '+ipAddr2string(con.adr)+' '+BStr(con.prt));
        exit;
        end;
      con.time:=CurrentTime;
      end;
    inc(con.ibufP);
    i:=con.ibufD[con.ibufP];
    con.cmd:=copy(con.cmd+chr(i),2,255);
    if con.sawNL then begin;
      con.sawNL:=False;
      if (i=$2e) then goto f1;
      end;
    con.rxb:=con.rxb+chr(i);
    if (length(con.rxb)>=250) then begin;
      xBlockWrite(con.fileH,con.rxb[1],length(con.rxb));
      con.rxb:='';
      end;
    if (i<>10) then goto f1;
    con.sawNL:=True;
    if (con.cmd<>#13#10'.'#13#10) then goto f1;
    xBlockWrite(con.fileH,con.rxb[1],length(con.rxb));
    xSeek(con.fileH,xFileSize(con.fileH)-2);
    xTruncate(con.fileH);
    xClose(con.fileH);
    con.fileH:=0;
    con.fileN:='';
    con.cmd:='';
    con.rxb:='';
    con.stat:=2;
    sendResp('250 message accepted');
    end;
  2:begin; {commands}
    b:=getNextCommand;
    if (b='') then exit;
    a:=kicsi(getWord(b));
    if (a='quit') then begin;
      sendResp('221 smtp server says goodbye');
      con.stat:=0;
      exit;
      end;
    if (a='mail') then begin;
      i:=pos(':',b);
      a:=' '+kicsi(copy(b,1,i-1))+' ';
      b:=' '+copy(b,i+1,255)+' ';
      kicserel('  ',' ',a);
      kicserel('  ',' ',b);
      b:=copy(b,2,length(b)-2);
      if (a<>' from ') then begin;
        sendResp('501 parameter unrecognized');
        exit;
        end;
      if (pos(' ',b)<>0) then begin;
        sendResp('501 parameter unrecognized');
        exit;
        end;
      EmailAddrDeQuote(b);
      if (b<>'') then if EmailAddrTestSyntax(b) then begin;
        sendResp('550 syntax error; <'+b+'>');
        exit;
        end;
      xClose(con.fileH);
      xErase(TempPath+con.fileN+MsgdatExt);
      xErase(TempPath+con.fileN+LocalExt);
      xErase(TempPath+con.fileN+RemoteExt);
      con.fileH:=0;
      con.fileN:='';
      BeginRequest;
      if (xtOpen(t,TempPath+con.fileN+RemoteExt,false)<>0) then begin;
        sendResp('451 local error');
        exit;
        end;
      xtWriteLn(t,b);
      xtClose(t);
      sendResp('250 sender ok; <'+b+'>');
      exit;
      end;
    if (a='rcpt') then begin;
      i:=pos(':',b);
      a:=' '+kicsi(copy(b,1,i-1))+' ';
      b:=' '+copy(b,i+1,255)+' ';
      kicserel('  ',' ',a);
      kicserel('  ',' ',b);
      b:=copy(b,2,length(b)-2);
      if (a<>' to ') then begin;
        sendResp('501 parameter unrecognized');
        exit;
        end;
      if (pos(' ',b)<>0) then begin;
        sendResp('501 parameter unrecognized');
        exit;
        end;
      if (con.fileN='') then begin;
        sendResp('503 bad sequence of commands');
        exit;
        end;
      EmailAddrDeQuote(b);
      if EmailAddrTestSyntax(b) then begin;
        sendResp('550 syntax error; <'+b+'>');
        exit;
        end;
      p:=FindEmailAddr(b);
      if (p=0) then p:=FindEmailAddr(EmailAddrGetDomain(b));
      if (p=0) then p:=FindEmailAddr('@');
      if (p=0) then if not con.relay then begin;
        sendResp('550 no such user here; <'+b+'>');
        exit;
        end;
      if (p=0) then begin;
        if (xtOpen(t,TempPath+con.fileN+RemoteExt,false)<>0) then begin;
          sendResp('451 local error');
          exit;
          end;
        xtWriteLn(t,b);
        xtClose(t);
        end else begin;
        if (xtOpen(t,TempPath+con.fileN+LocalExt,false)<>0) then begin;
          sendResp('451 local error');
          exit;
          end;
        xtWriteLn(t,BStr(p)+' '+b);
        xtClose(t);
        end;
      sendResp('250 recipient ok; <'+b+'>');
      exit;
      end;
    if (a='data') then begin;
      if (b<>'') then begin;
        sendResp('501 parameter unrecognized');
        exit;
        end;
      if (con.fileN='') then begin;
        sendResp('503 bad sequence of commands');
        exit;
        end;
      if (xtOpen(t,TempPath+con.fileN+RemoteExt,true)<>0) then begin;
        sendResp('451 local error');
        exit;
        end;
      xtReadLn(t,255);
      b:=xtReadLn(t,255);
      xtClose(t);
      if (xtOpen(t,TempPath+con.fileN+LocalExt,true)<>0) then begin;
        sendResp('451 local error');
        exit;
        end;
      b:=b+xtReadLn(t,255);
      xtClose(t);
      if (b='') then begin;
        sendResp('503 bad sequence of commands');
        exit;
        end;
      if (xtOpen(t,TempPath+con.fileN+MsgdatExt,false)<>0) then begin;
        sendResp('451 local error');
        exit;
        end;
      xtSetPos(t,0);
      a:=con.remote;
      if (a<>'') then a:=' (helo '+a+')';
      xtWriteLn(t,'Received: from '+ipAddr2string(con.adr)+a+' with smtp via tcp/ip');
      xtWriteLn(t,'          by '+ipAddr2string(serverAddr)+' (helo '+DomainName+');');
      xtWriteLn(t,'          '+GetDateForHeader);
      if (xtOpen(tt,TempPath+con.fileN+LocalExt,true)<>0) then begin;
        sendResp('451 local error');
        exit;
        end;
      while not xtEOF(tt) do begin;
        b:=xtReadLn(tt,255);
        b:=copy(b,pos(' ',b)+1,255);
        xtWriteLn(t,'          for '+b);
        end;
      xtClose(tt);
      if (xtOpen(tt,TempPath+con.fileN+RemoteExt,true)<>0) then begin;
        xtClose(t);
        sendResp('451 local error');
        exit;
        end;
      xtReadLn(tt,255);
      while not xtEOF(tt) do begin;
        b:=xtReadLn(tt,255);
        xtWriteLn(t,'          for '+b);
        end;
      xtSetPos(tt,0);
      b:=xtReadLn(tt,255);
      xtClose(tt);
      xtWriteLn(t,'          (envelope-from '+b+')');
      xtClose(t);
      if (xOpen(con.fileH,TempPath+con.fileN+MsgdatExt,xGenFilMod_rw)<>0) then begin;
        sendResp('451 local error');
        exit;
        end;
      xSeek(con.fileH,xFileSize(con.fileH));
      con.stat:=3;
      con.cmd:='12345';
      con.rxb:='';
      con.sawNL:=True;
      sendResp('354 start mail input; end with <CRLF>.<CRLF>');
      exit;
      end;
    if (a='vrfy') then begin;
      p:=FindEmailAddr(b);
      if (p=0) then p:=FindEmailAddr(EmailAddrGetDomain(b));
      if (p=0) then begin;
        sendResp('550 no such user here; <'+b+'>');
        exit;
        end;
      sendResp('250 <'+b+'>');
      exit;
      end;
    if (a='expn') then begin;
      sendResp('550 not a mailing list; <'+b+'>');
      exit;
      end;
    if (a='helo') then begin;
      con.remote:=b;
      sendResp('250 '+DomainName);
      exit;
      end;
    if (a='ehlo') then begin;
      con.remote:=b;
      sendResp('250-'+DomainName);
      sendResp('250 HELP');
      exit;
      end;
    if (a='auth') then begin;
      a:=kicsi(getWord(b));
      if (a='plain') then begin;
        sendResp('334 send authentication data!');
        con.stat:=4;
        exit;
        end;
      sendResp('500 command unrecognized: "auth '+a+'" "'+b+'"');
      exit;
      end;
    if (a='help') then begin;
      sendResp('214-server: '+ipAddr2string(serverAddr)+' '+BStr(serverPort)+' [helo '+DomainName+']');
      a:=con.remote;
      if (a<>'') then a:=' [helo '+a+']';
      sendResp('214-client: '+ipAddr2string(con.adr)+' '+BStr(con.prt)+a);
      if con.relay then a:='allowed' else a:='forbidden';
      sendResp('214-forwarding: '+a);
      sendResp('214 commands: helo ehlo quit noop help auth_plain expn vrfy rset mail_from rcpt_to data');
      exit;
      end;
    if (a='noop') then begin;
      sendResp('250 no operation ok');
      exit;
      end;
    if (a='rset') then begin;
      xClose(con.fileH);
      xErase(TempPath+con.fileN+MsgdatExt);
      xErase(TempPath+con.fileN+LocalExt);
      xErase(TempPath+con.fileN+RemoteExt);
      con.fileH:=0;
      con.fileN:='';
      sendResp('250 reset ok');
      exit;
      end;

    sendResp('500 command unrecognized: "'+a+'" "'+b+'"');
    end;
  4:begin; {auth}
    b:=getNextCommand;
    if (b='') then exit;
    con.relay:=false;
    b:=copy(decodeBase64(b),2,666);
    i:=pos(#0,b);
    a:=copy(b,i+1,666);
    b:=copy(b,1,i-1);
    b:=getPasswordOfUser(b);
    if (b<>'') then if (a=b) then con.relay:=true;
    if con.relay then a:='successful' else a:='failed';
    sendResp('235 authentication '+a);
    con.stat:=2;
    end;
  1:begin; {init}
    if TCPlookConnected(con.pipe,con.adr,con.prt,i) then begin;
      doConn:=(con.pipe=0);
      exit;
      end;
    WriteLn('connection accepted from '+ipAddr2string(con.adr)+' '+BStr(con.prt));
    con.relay:=false;
    for i:=1 to relayListNum do if TestAddrMask(con.adr,relayListDat[i].adr,relayListDat[i].msk) then begin;
      con.relay:=true;
      break;
      end;
    if relaySelfAddr then if TCPcompareAddress(con.adr,serverAddr) then con.relay:=true;
    sendResp('220 smtp server ready!');
    con.stat:=2;
    con.cmd:='';
    end;
  0:begin; doConn:=True;exit; end; {disconnect}
  else begin; doConn:=True;exit; end;
  end;
End;
