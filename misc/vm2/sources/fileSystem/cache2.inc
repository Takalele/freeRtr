Const CacheDataMax=32;
Var
  CacheDataPos:LongInt;
  CacheDataBuf:array[1..CacheDataMax] of OneSectorRecord;
  CacheDataNum:array[1..CacheDataMax] of LongInt;
  CacheDataFls:array[1..CacheDataMax] of Boolean;

Procedure CacheInit;
Begin;
CacheDataPos:=0;
fillchar(CacheDataBuf,sizeof(CacheDataBuf),0);
fillchar(CacheDataNum,sizeof(CacheDataNum),$ff);
fillchar(CacheDataFls,sizeof(CacheDataFls),0);
End;

Function CacheFlush:Word;
Var i,o:longInt;
Begin;
CacheFlush:=0;
for o:=1 to CacheDataMax do if CacheDataFls[o] then begin;
  i:=DriveWrite(CacheDataNum[o],CacheDataBuf[o]);
  if (i<>0) then CacheFlush:=i;
  end;
fillchar(CacheDataFls,sizeof(CacheDataFls),0);
End;

Function CacheRead(sec:longint;var buf):Word;
Var i:longInt;
Begin;
for i:=1 to CacheDataMax do if (CacheDataNum[i]=sec) then begin;
  move(CacheDataBuf[i],buf,sizeof(OneSectorRecord));
  CacheRead:=0;
  exit;
  end;
CacheDataPos:=(CacheDataPos mod CacheDataMax)+1;
if CacheDataFls[CacheDataPos] then begin;
  CacheDataFls[CacheDataPos]:=False;
  i:=DriveWrite(CacheDataNum[CacheDataPos],CacheDataBuf[CacheDataPos]);
  if (i<>0) then begin; CacheRead:=i;exit; end;
  end;
i:=DriveRead(sec,buf);
if (i<>0) then begin; CacheRead:=i;exit; end;
move(buf,CacheDataBuf[CacheDataPos],sizeof(OneSectorRecord));
CacheDataNum[CacheDataPos]:=sec;
CacheDataFls[CacheDataPos]:=False;
CacheRead:=0;
End;

Function CacheWrite(sec:longint;var buf):Word;
Var i:longInt;
Begin;
if DriveReadOnly then begin; CacheWrite:=4;exit; end;
for i:=1 to CacheDataMax do if (CacheDataNum[i]=sec) then begin;
  move(buf,CacheDataBuf[i],sizeof(OneSectorRecord));
  CacheDataFls[i]:=True;
  CacheWrite:=0;
  exit;
  end;
CacheDataPos:=(CacheDataPos mod CacheDataMax)+1;
if CacheDataFls[CacheDataPos] then begin;
  CacheDataFls[CacheDataPos]:=False;
  i:=DriveWrite(CacheDataNum[CacheDataPos],CacheDataBuf[CacheDataPos]);
  if (i<>0) then begin; CacheWrite:=i;exit; end;
  end;
move(buf,CacheDataBuf[CacheDataPos],sizeof(OneSectorRecord));
CacheDataNum[CacheDataPos]:=sec;
CacheDataFls[CacheDataPos]:=True;
CacheWrite:=0;
End;
